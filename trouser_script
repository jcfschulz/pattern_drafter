name Basic Trouser Draft following Rundschau XVI Ed

measure side_seam Sideseam 108
measure inseam Inseam 80
measure waist Waist 84
measure seat Seat 100
measure hem Hem 42

extrapar hem_corr is 1.
extrapar stance_corr is 0.
extrapar waist_corr is -1.
extrapar heel_corr is 4.
extrapar front_hem_corr is 1.
extrapar g_corr is 2.
extrapar G1_corr is 1.
extrapar G_corr is 3.
extrapar b_left_corr is 0.75
extrapar S_left_corr is 1.5
extrapar n_right_corr is 1.

extrapar back_hem is 2.5
extrapar back_knee is 3
extrapar back_knee_corr is 1
extrapar back_M_corr is 1
extrapar back_seat_allowance is 5
extrapar crookness_angle_corr is -1.5
extrapar waistband_corr is 1.
extrapar back_point_allowance_G is 1.
extrapar back_point_allowance_S is 1/2.
extrapar waistband_allowance is 3
extrapar excess_corr is 0.5

extrapar short_dart_length is 5.
extrapar long_dart_length is 10.
extrapar short_dart_height is 0.75
extrapar long_dart_height is 0.5


extrapar b1_move is (1./sqrt(5), 2./sqrt(5))


start FrontTrouser

point L is (0.,0.)
line LB from L up
point K on LB from L distance inseam / 10. + inseam / 2.
point S on LB from L distance inseam
point B on LB from L distance side_seam
point G on LB from S distance seat/2./10.+G_corr
point L1 on LB from L distance heel_corr
line L1_line from L1 right
line K_line from K right
line S_line from S right
line B_line from B right
line G_line from G right
point b_ on B_line from B distance 2*(seat-waist)/10.
point B1 on B_line from b_ distance waist/4.
line B1_line from B1 down
point g intersect B1_line G_line
point S1 intersect B1_line S_line
point G1 on G_line from g distance seat/2./10.+G1_corr
point M on G_line from G distance dist(G,G1)/2.
line M_line from M up
point M1 intersect M_line B_line
point M2 intersect M_line L1_line
move M2 stance_corr*right
point a on L1_line from M2 distance -(hem/2.-hem_corr)/2.
point b on L1_line from M2 distance (hem/2.-hem_corr)/2.
point ac on L1_line from a distance front_hem_corr
point bc on L1_line from b distance -front_hem_corr
line ac_G from ac to G
point K1 intersect K_line ac_G
line bc_G1 from bc to G1
point K2 intersect K_line bc_G1
point S2 intersect S_line bc_G1
point g_prime on B1_line from g distance g_corr
line g_prime_S2 from g_prime to S2
line n_line normal S1 g_prime_S2
point n intersect n_line g_prime_S2
point b1 on B_line from b_ distance waist_corr
point b2 on B_line from B1 distance b_left_corr
point S3 on S_line from S2 distance S_left_corr
point nr on n_line from n distance n_right_corr


line a_line from a up
point outseam_control_1 intersect a_line ac_G
point outseam_control_2 on ac_G from K1 distance -dist(G,K1)/3. 
seamline front_outseam a outseam_control_1 outseam_control_1 K1
seamline front_outseam K1 outseam_control_2 S G
point b1_prime is b1.p
move b1_prime b1_move
point outseam_control_3 on LB from G distance 2*dist(G,B)/5. 
point outseam_control_4 on LB from G distance 3*dist(G,B)/5. 
seamline front_outseam G outseam_control_3 outseam_control_4 b1_prime

point topseam_control_1 on B_line from b_ distance 4. 
seamline front_topseam b1_prime topseam_control_1 M1 B1
seamline front_topseam B1 b2

line b_line from b up
point inseam_control_1 intersect b_line bc_G1
seamline front_inseam b inseam_control_1 inseam_control_1 K2
seamline front_inseam_right K2 S2
point inseam_control_3 on bc_G1 from K2 distance -dist(S2,K2)/2. 
seamline front_inseam_left K2 inseam_control_3 S3 S3

point crotchseam_control_1 on B1_line from g_prime distance 2*dist(g,S1)/5.
seamline front_crotchseam_right B1 g crotchseam_control_1 S2
point g2 on G_line from g distance b_left_corr
line b2_line from b2 down
point crotchseam_control_2 is crotchseam_control_1.p
move crotchseam_control_2 S_left_corr*right
seamline front_crotchseam_left b2 g2 crotchseam_control_2 S3

seamline front_hemseam a b

################################################################################

start BackTrouser

sheet BackTrouser add a b M2
sheet BackTrouser add K1 K2
sheet BackTrouser add S S1 S2 S3
sheet BackTrouser add M
sheet BackTrouser add M1 B1 b_ b1 b1_prime
sheet BackTrouser add L1_line K_line S_line G_line B_line M_line

sheet BackTrouser show front_outseam front_inseam_left front_topseam front_inseam front_inseam_right front_crotchseam_left front_crotchseam_right front_hemseam

point M3 intersect K_line M_line

point c on L1_line from a distance -back_hem
point c_prime on L1_line from c distance hem_corr
point d on L1_line from b distance back_hem
point d_prime on L1_line from d distance -hem_corr
point K3 on K_line from K1 distance -back_knee
point K3_prime on K_line from K3 distance -back_knee_corr
point K4 on K_line from K2 distance back_knee
point K4_prime on K_line from K4 distance back_knee_corr
line c_K3 from c_prime to K3_prime
line d_K4 from d_prime to K4_prime

point m on G_line from M distance back_M_corr
point S6 on G_line from m distance (seat/4. + back_seat_allowance)/4.

point S_temp intersect ac_G S_line
point S5 on ac_G from S_temp distance crookness_angle_corr
line S5_S6 from S5 to S6
line S6_B5 normal S6 S5_S6
point B3 intersect S6_B5 B_line

point G2 on S6_B5 from B3 distance dist(B3,S6)/2.
point G3 from G2 on G_line distance min (seat/4. + back_seat_allowance)
line G2_G3 from G2 to G3
line K3_G3 from K3 to G3

point B4 on K3_G3 from K3 distance -dist(K1,b1_prime)
point B5 from M3 on S6_B5 distance min dist(B4,M3)
point B6 on S6_B5 from B5 distance waistband_corr
line B4_B6 from B4 to B6

point G4 on G_line from m distance dist(m,G3)+back_point_allowance_G
line G4_K4 from G4 to K4

point S4 intersect S_line G4_K4
point S4_prime on G4_K4 from S4 distance -back_point_allowance_S

point B7 on B4_B6 from B6 distance waist/4.+waistband_allowance

point B4_prime on B4_B6 from B4 distance -excess_corr
point B6_prime on B4_B6 from B6 distance excess_corr

point back_outseam_control_1 on c_K3 from c_prime distance -10
point back_outseam_control_2 on c_K3 from K3_prime distance 10
seamline back_outseam c back_outseam_control_1 back_outseam_control_2 K3
point back_outseam_control_3 is K3.p
move back_outseam_control_3 7.5*up
point back_outseam_control_4 on K3_G3 from G3 distance 15
seamline back_outseam K3 back_outseam_control_3 back_outseam_control_4 G3
point back_outseam_control_5 on K3_G3 from B4 distance 10
seamline back_outseam G3 back_outseam_control_5 B4_prime B4_prime

point B4_prime_control on B4_B6 from B4_prime distance -2
point B6_prime_control on B4_B6 from B6_prime distance 5


point dart1 on B4_B6 from B4 distance -dist(B4_prime, B6_prime)/4.
line dart1_line normal dart1 B4_B6
point dart1_middle on dart1_line from dart1 distance short_dart_length
point dart1_start is dart1.p
move dart1_start (dist(B4,B7)-2*excess_corr)/6.*B4_B6.u
move dart1_start -short_dart_height*normal(B4_B6.u)
point dart1_start_control is dart1_start.p
move dart1_start_control 2*B4_B6.u
point dart1_end is dart1.p
move dart1_end -(dist(B4,B7)-2*excess_corr)/6.*B4_B6.u
move dart1_end -short_dart_height*normal(B4_B6.u)
point dart1_end_control is dart1_end.p
move dart1_end_control -2*B4_B6.u

point dart2 on B4_B6 from B4 distance -dist(B4_prime, B6_prime)/2.
line dart2_line normal dart2 B4_B6
point dart2_middle on dart2_line from dart2 distance long_dart_length
point dart2_start is dart2.p
move dart2_start (dist(B4,B7)-2*excess_corr)/3.*B4_B6.u
move dart2_start -long_dart_height*normal(B4_B6.u)
point dart2_start_control is dart2_start.p
move dart2_start_control 2*B4_B6.u
point dart2_end is dart2.p
move dart2_end -(dist(B4,B7)-2*excess_corr)/3.*B4_B6.u
move dart2_end -long_dart_height*normal(B4_B6.u)
point dart2_end_control is dart2_end.p
move dart2_end_control -4*B4_B6.u

seamline back_topseam B4_prime B4_prime_control dart1_start_control dart1_start
seamline back_topseam dart1_start dart1_middle
seamline back_topseam dart1_middle dart1_end
seamline back_topseam dart1_end dart1_end_control dart2_start_control dart2_start
seamline back_topseam dart2_start dart2_middle
seamline back_topseam dart2_middle dart2_end
seamline back_topseam dart2_end dart2_end_control B6_prime_control B6_prime

seamline back_crotchseam B6_prime G2
seamline back_crotchseam G2 S6
point back_crotchseam_control_1 on S6_B5 from S6 distance 5
point back_crotchseam_control_2 is S2.p
move back_crotchseam_control_2 5*left
move back_crotchseam_control_2 2*up
seamline back_crotchseam S6 back_crotchseam_control_1 back_crotchseam_control_2 S2
line back_crotchseam_control_line from back_crotchseam_control_2 to S2
point back_crotchseam_control_3 on back_crotchseam_control_line from S2 distance -4
seamline back_crotchseam S2 back_crotchseam_control_3 S4_prime S4_prime

point back_inseam_control_1 on d_K4 from d_prime distance -10
point back_inseam_control_2 on d_K4 from K4_prime distance 20
seamline back_inseam d back_inseam_control_1 back_inseam_control_2 K4
point back_inseam_control_3 is K4.p
move back_inseam_control_3 10*up
move back_inseam_control_3 1*right
point back_inseam_control_4 is S4.p
move back_inseam_control_4 10*down
move back_inseam_control_4 4*left
seamline back_inseam K4 back_inseam_control_3 back_inseam_control_4 S4_prime

seamline back_hemseam c d

